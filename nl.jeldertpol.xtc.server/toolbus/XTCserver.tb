process XTC is
	printf("TB: script is up and running...\n") .

	let
		Server : server,
		Client : client,
		Cid : int
	in
		(
			rec-connect(Server?)
			. printf("TB: XTC server connected...\n")
		)
		.
		(
			(
				rec-connect(Client?)
				. printf("TB: XTC client connected...\n")
				. create(XTCclient(Client, Server), Cid?)
			) * delta
		)
	endlet

// There is only one server, so now we can send directly to the server
process XTCclient(Client : client, Server : server) is
	printf("client\n") .
	
	(
		// Get current sessions
		let
			Sessions : term
		in
			rec-request(Client, getSessions())
			. snd-eval(Server, getSessions())
			. rec-value(Server, getSessions(Sessions?))
			. snd-response(Client, getSessions(Sessions))
		endlet
	)
	*
	(
		(
			// Start a new session
			let
				Project : str,
				Revision : term,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, startSession(Project?, Revision?, Nickname?))
				. snd-eval(Server, startSession(Project, Revision, Nickname))
				. rec-value(Server, startSession(Success?))
				. subscribe(change(<str>, <str>, <int>, <int>, <str>, <str>))	// Project, Filename, Length, Offset, Text, Nickname
				. subscribe(move(<str>, <str>, <str>, <str>))	// Project, From, To, Nickname
				. subscribe(content(<str>, <str>, <term>, <str>))	// Project, File, Content, Nickname
				. subscribe(addedResource(<str>, <str>, <int>, <str>))	// Project, Resource, Type, Nickname
				. subscribe(removedResource(<str>, <str>, <str>))	// Project, Resource, Nickname
				. snd-response(Client, startSession(Success))
			endlet
		)
		+
		(
			// Join an existing session
			let
				Project : str,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, joinSession(Project?, Nickname?))
				. snd-eval(Server, joinSession(Project, Nickname))
				. rec-value(Server, joinSession(Success?))
				. subscribe(change(<str>, <str>, <int>, <int>, <str>, <str>))	// Project, Filename, Length, Offset, Text, Nickname
				. subscribe(move(<str>, <str>, <str>, <str>))	// Project, From, To, Nickname
				. subscribe(content(<str>, <str>, <term>, <str>))	// Project, File, Content, Nickname
				. subscribe(addedResource(<str>, <str>, <int>, <str>))	// Project, Resource, Type, Nickname
				. subscribe(removedResource(<str>, <str>, <str>))	// Project, Resource, Nickname
				. snd-response(Client, joinSession(Success))
			endlet
		)
		+
		(
			// Leave a session
			let
				Project : str,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, leaveSession(Project?, Nickname?))
				. snd-eval(Server, leaveSession(Project, Nickname))
				. rec-value(Server, leaveSession(Success?))
				. unsubscribe(change(<str>, <str>, <int>, <int>, <str>, <str>))	// Project, Filename, Length, Offset, Text, Nickname
				. snd-response(Client, leaveSession(Success))
			endlet
		)
		+
		(
			// Send a change
			let
				Project : str,
				Filename : str,
				Length : int,
				Offset : int,
				Text : str,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, sendChange(Project?, Filename?, Length?, Offset?, Text?, Nickname?))
				. snd-eval(Server, sendChange(Project, Filename, Length, Offset, Text, Nickname))
				. rec-value(Server, sendChange(Success?))
				. snd-response(Client, sendChange(Success))
				. snd-note(change(Project, Filename, Length, Offset, Text, Nickname))
			endlet
		)
		+
		(
			// Receive a change
			let
				Project : str,
				Filename : str,
				Length : int,
				Offset : int,
				Text : str,
				Nickname : str,
				Success : bool
			in
				rec-note(change(Project?, Filename?, Length?, Offset?, Text?, Nickname?))
				. snd-do(Client, receiveChange(Project, Filename, Length, Offset, Text, Nickname))
			endlet
		)
		+
		(
			// Send a move
			let
				Project : str,
				From : str,
				To : str,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, sendMove(Project?, From?, To?, Nickname?))
				. snd-eval(Server, sendMove(Project, From, To, Nickname))
				. rec-value(Server, sendMove(Success?))
				. snd-response(Client, sendMove(Success))
				. snd-note(move(Project, From, To, Nickname))
			endlet
		)
		+
		(
			// Receive a move
			let
				Project : str,
				From : str,
				To : str,
				Nickname : str
			in
				rec-note(move(Project?, From?, To?, Nickname?))
				. snd-do(Client, receiveMove(Project, From, To, Nickname))
			endlet
		)
		+
		(
			// Send content
			let
				Project : str,
				Filename : str,
				Content : term,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, sendContent(Project?, Filename?, Content?, Nickname?))
				. snd-eval(Server, sendContent(Project, Filename, Content, Nickname))
				. rec-value(Server, sendContent(Success?))
				. snd-response(Client, sendContent(Success))
				. snd-note(content(Project, Filename, Content, Nickname))
			endlet
		)
		+
		(
			// Receive content
			let
				Project : str,
				Filename : str,
				Content : term,
				Nickname : str
			in
				rec-note(content(Project?, Filename?, Content?, Nickname?))
				. snd-do(Client, receiveContent(Project, Filename, Content, Nickname))
			endlet
		)
		+
		(
			// Send added resource
			let
				Project : str,
				Resource : str,
				Type : int,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, sendAddedResource(Project?, Resource?, Type?, Nickname?))
				. snd-eval(Server, sendAddedResource(Project, Resource, Type, Nickname))
				. rec-value(Server, sendAddedResource(Success?))
				. snd-response(Client, sendAddedResource(Success))
				. snd-note(addedResource(Project, Resource, Type, Nickname))
			endlet
		)
		+
		(
			// Receive added resource
			let
				Project : str,
				Resource : str,
				Type : int,
				Nickname : str
			in
				rec-note(addedResource(Project?, Resource?, Type?, Nickname?))
				. snd-do(Client, receiveAddedResource(Project, Resource, Type, Nickname))
			endlet
		)
		+
		(
			// Send removed resource
			let
				Project : str,
				Resource : str,
				Nickname : str,
				Success : bool
			in
				rec-request(Client, sendRemovedResource(Project?, Resource?, Nickname?))
				. snd-eval(Server, sendRemovedResource(Project, Resource, Nickname))
				. rec-value(Server, sendRemovedResource(Success?))
				. snd-response(Client, sendRemovedResource(Success))
				. snd-note(removedResource(Project, Resource, Nickname))
			endlet
		)
		+
		(
			// Receive removed resource
			let
				Project : str,
				Resource : str,
				Nickname : str
			in
				rec-note(removedResource(Project?, Resource?, Nickname?))
				. snd-do(Client, receiveRemovedResource(Project, Resource, Nickname))
			endlet
		)
	)
	*	
	rec-disconnect(Client)

tool server is {}
tool client is {}

toolbus(XTC)
