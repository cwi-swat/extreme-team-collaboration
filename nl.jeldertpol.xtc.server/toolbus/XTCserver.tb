process XTC is
	printf("TB: script is up and running...\n") .

	let
		Server : server,
		Client : client,
		Cid : int
	in
		(
			rec-connect(Server?)
			. printf("TB: XTC server connected...\n")
		)
		.
		(
			(
				rec-connect(Client?)
				. printf("TB: XTC client connected...\n")
				. create(XTCclient(Client, Server), Cid?)
			) * delta
		)
	endlet

// There is only one server, so now we can send directly to the server
process XTCclient(Client : client, Server : server) is
	printf("client\n") .
	
	(	
		(
			// Get current sessions
			let
				Sessions : term
			in
				rec-request(Client, getSessions())
				. snd-eval(Server, getSessions())
				. rec-value(Server, getSessions(Sessions?))
				. snd-response(Client, getSessions(Sessions))
			endlet
		)
		*
		(
			(
				// Start a new session
				let
					Project : str,
					Revision : term,
					Nickname : str,
					Succes : bool
				in
					rec-request(Client, startSession(Project?, Revision?, Nickname?))
					. snd-eval(Server, startSession(Project, Revision, Nickname))
					. rec-value(Server, startSession(Succes?))
					. subscribe(change(<str>, <term>))	// project, change
					. snd-response(Client, startSession(Succes))
				endlet
			)
			+
			(
				// Join an existing session
				let
					Project : str,
					Nickname : str,
					Succes : bool
				in
					rec-request(Client, jointSession(Project?, Nickname?))
					. snd-eval(Server, joinSession(Project, Nickname))
					. rec-value(Server, joinSession(Succes?))
					. subscribe(change(<str>, <term>))	// project, change
					. snd-response(Client, joinSession(Succes))
				endlet
			)
			*
			(
				// Leave a session
				let
					Project : str,
					Nickname : str,
					Succes : bool
				in
					rec-request(Client, leaveSession(Project?, Nickname?))
					. snd-eval(Server, leaveSession(Project, Nickname))
					. rec-value(Server, leaveSession(Succes?))
					. unsubscribe(change(<str>, <term>))	// project, change
					. snd-response(Client, leaveSession(Succes))
				endlet
			)
		)
	)
	*
	rec-disconnect(Client)

tool server is {}
tool client is {}

toolbus(XTC)
