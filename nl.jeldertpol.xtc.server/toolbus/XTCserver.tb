process XTC is
	printf("TB: script is up and running...\n") .

	let
		Server : server,
		Client : client,
		Cid : int
	in
		(
			rec-connect(Server?)
			. printf("TB: XTC server connected...\n")
		)
		.
		(
			(
				rec-connect(Client?)
				. printf("TB: XTC client connected...\n")
				. create(XTCclient(Client, Server), Cid?)
			) * delta
		)
	endlet

// There is only one server, so now we can send directly to the server
process XTCclient(Client : client, Server : server) is
	printf("client\n") .
	
	let
		NicknameCheck : str	// Used so changes are not send to client itself.
	in
		(
			(
				// Get current sessions
				let
					Sessions : blob
				in
					rec-request(Client, getSessions())
					. snd-eval(Server, getSessions())
					. rec-value(Server, getSessions(Sessions?))
					. snd-response(Client, getSessions(Sessions))
				endlet
			)
			+
			(
				// Start or join a session
				let
					Project : str,
					Revision : term,
					Nickname : str,
					Success : blob
				in
					rec-request(Client, startJoinSession(Project?, Revision?, Nickname?))
					. snd-eval(Server, startJoinSession(Project, Revision, Nickname))
					. rec-value(Server, startJoinSession(Success?))
					. NicknameCheck := Nickname
					. subscribe(change(<str>, <term>, <str>))	// Project, Change, Nickname
					. subscribe(chat(<blob>))	// ChatMessage
					. subscribe(whosWhere(<str>, <str>, <str>))	//Nickname, Project, Resource
					. snd-response(Client, startJoinSession(Success))
				endlet
			)
			+
			(
				// Leave a session
				let
					Project : str,
					Nickname : str,
					Success : bool
				in
					rec-request(Client, leaveSession(Project?, Nickname?))
					. snd-eval(Server, leaveSession(Project, Nickname))
					. rec-value(Server, leaveSession(Success?))
					. unsubscribe(change(<str>, <term>, <str>))	// Project, Change, Nickname
					. unsubscribe(chat(<blob>))	// ChatMessage
					. unsubscribe(whosWhere(<str>, <str>, <str>))	//Nickname, Project, Resource
					. snd-response(Client, leaveSession(Success))
				endlet
			)
			+
			(
				// Request changes in session
				let
					Project : str,
					Changes : blob
				in
					rec-request(Client, requestChanges(Project?))
					. snd-eval(Server, requestChanges(Project))
					. rec-value(Server, requestChanges(Changes?))
					. snd-response(Client, requestChanges(Changes))
				endlet
			)
			+
			(
				// Request textual changes of resource
				let
					Project : str,
					Resource : str,
					Changes : blob
				in
					rec-request(Client, requestTextualChanges(Project?, Resource?))
					. snd-eval(Server, requestTextualChanges(Project, Resource))
					. rec-value(Server, requestTextualChanges(Changes?))
					. snd-response(Client, requestTextualChanges(Changes))
				endlet
			)
			+
			(
				// Send a change
				let
					Project : str,
					Change : blob,
					Nickname : str,
					Success : bool
				in
					rec-request(Client, sendChange(Project?, Change?, Nickname?))
					. snd-eval(Server, sendChange(Project, Change, Nickname))
					. rec-value(Server, sendChange(Success?))
					. snd-response(Client, sendChange(Success))
					. snd-note(change(Project, Change, Nickname))
				endlet
			)
			+
			(
				// Receive a change
				let
					Project : str,
					Change : blob,
					Nickname : str
				in
					rec-note(change(Project?, Change?, Nickname?))
					// Prevent sending change back to sender
					. if not-equal(NicknameCheck, Nickname) then
						snd-do(Client, receiveChange(Project, Change, Nickname))
					else
						tau
					fi
				endlet
			)
			+
			(
				// Send a chat message
				let
					ChatMessage : blob,
					ChatMessageProcessed : blob
				in
					rec-event(Client, sendChat(ChatMessage?))
					. snd-eval(Server, sendChat(ChatMessage))
					. rec-value(Server, sendChat(ChatMessageProcessed?))
					. snd-ack-event(Client, sendChat(ChatMessage))
					. snd-note(chat(ChatMessageProcessed))
				endlet
			)
			+
			(
				// Receive a chat message
				let
					ChatMessage : blob
				in
					rec-note(chat(ChatMessage?))
					. snd-do(Client, receiveChat(ChatMessage))
				endlet
			)
			+
			(
				// Send WhosWhere information
				let
					Nickname : str,
					Project : str,
					Resource : str
				in
					rec-event(Client, sendWhosWhere(Nickname?, Project?, Resource?))
					. snd-do(Server, whosWhere(Nickname, Project, Resource))
					. snd-note(whosWhere(Nickname, Project, Resource))
					. snd-ack-event(Client, sendWhosWhere(Nickname, Project, Resource))
				endlet
			)
			+
			(
				// Receive WhosWhere information
				let
					Nickname : str,
					Project : str,
					Resource : str
				in
					rec-note(whosWhere(Nickname?, Project?, Resource?))
					. snd-do(Client, receiveWhosWhere(Nickname, Project, Resource))
				endlet
			)
		)
		*
		rec-disconnect(Client)
	endlet

tool server is {}
tool client is {}

toolbus(XTC)
